#include <common.h>

// Samplers
sampler depthSampler : register(s0);
sampler normalSampler : register(s1);
sampler diffuseSampler : register(s2);
sampler pointProjectionSampler : register(s3);
sampler spotProjectionSampler : register(s4);
sampler shadowSampler : register(s5);
sampler cubeShadowSampler : register(s6);

// Float constants
float4x4 invViewProj : register( c0 );
float4x4 spotViewProjMatrix : register( c4 );
float3 lightPos : register( c8 );
float3 lightColor : register( c9 );
float lightRange : register( c10 );
float lightIntensity : register(c11);
float3 cameraPos : register(c12);
float3 direction : register(c13);

// Bool constants
bool usePointLighting : register( b0 );
bool useShadows : register( b1 );
bool useDirectionalLighting : register(b2);
bool highDynamicRange : register(b3);

static const float3 sampleOffsetDirections[20] = {
   float3( 1,  1,  1), float3( 1, -1,  1), float3(-1, -1,  1), float3(-1,  1,  1), 
   float3( 1,  1, -1), float3( 1, -1, -1), float3(-1, -1, -1), float3(-1,  1, -1),
   float3( 1,  1,  0), float3( 1, -1,  0), float3(-1, -1,  0), float3(-1,  1,  0),
   float3( 1,  0,  1), float3(-1,  0,  1), float3( 1,  0, -1), float3(-1,  0, -1),
   float3( 0,  1,  1), float3( 0, -1,  1), float3( 0, -1, -1), float3( 0,  1, -1)
}; 

// returns antialised shadow term for point light
float GetShadow(in float3 lightVector) {
	float shadow = 0.0;	
	int samples = 20;
	float diskRadius = 0.015;
	float currentDistance = length( lightVector );
	for(int i = 0; i < samples; ++i) {
		const float bias = 0.035;
		float3 samplingVector = lightVector + sampleOffsetDirections[i] * diskRadius;
		if(currentDistance - bias > texCUBElod(cubeShadowSampler, float4(-samplingVector, 0)).r) {
			shadow += 1.0;
		}
	}
	return saturate(1.2f - shadow / float(samples));  
}

float4 main( float2 texcoord : TEXCOORD0 ) : COLOR0 {
	// remap normal to [-1.0;+1.0]
	float3 n = normalize( tex2D( normalSampler, texcoord ).xyz * 2.0f - 1.0f );		
	float3 p = RestorePositionFromDepth(texcoord, invViewProj, depthSampler);
	
	// attenuation
	float3 lightVector = lightPos - p.xyz;
	float4 outputColor;	
	float3 diffuse = tex2D( diffuseSampler, texcoord ).xyz;

	if(useDirectionalLighting) {		
		outputColor = float4( lightColor.xyz * diffuse * max(0, dot(direction, n)), 1.0f);
	} else {		
		float attenuation = lightIntensity * GetAttenuation( lightVector, lightRange, n );		
		outputColor = float4( lightColor.xyz * diffuse * attenuation, 1.0f );			
	}
	const float bias = 0.002f;
	
	if( usePointLighting ) { 
		// Point lighting		
		outputColor *= texCUBElod( pointProjectionSampler, float4( lightVector, 0 ) );
		if( useShadows ) {
			outputColor.rgb *= GetShadow(lightVector);
		}
	} else {
		 // Spot lighting			 
		float4 projection = GetProjection( p.xyz, spotViewProjMatrix );		
		if(!useDirectionalLighting) {
			outputColor *= tex2Dlod( spotProjectionSampler, projection );						
		} 	
		if( useShadows ) {
		/*
			const float4 smStep = float4(1.0f / 1024.0f, 1.0f / 1024.0f, 0, 0);
			float shadowTerm = 0.0f;
			for(float y = -1.5f; y <= 1.5f; y += 1.0f) {
				for(float x = -1.5f; x <= 1.5f; x += 1.0f) {
					shadowTerm += tex2Dlod( shadowSampler, projection + smStep * float4(x, y, 0, 0)).r;
				}
			}
			shadowTerm /= 16;
			if( projection.z - bias > shadowTerm ) {
			   outputColor.rgb *= 0.25f;
			};*/
			if( projection.z - bias > tex2Dlod( shadowSampler, projection).r) {
			   outputColor.rgb *= 0.1f;
			};
		}		
	}

	return outputColor;
};